// --------------------------------------------------------------------------
//
// ARGENT: Weapon Mod Kit
//
// --------------------------------------------------------------------------

class ArgModKit : CustomInventory replaces Backpack
{
	bool active;

	Default
	{
		Scale 0.66;
		Inventory.PickupMessage "$ARG_GOT_MOD_KIT";
		Inventory.PickupSound "items/modkit/pickup";
		Inventory.MaxAmount 999;
		Inventory.Icon "graphics/hud/equip-icon-modkit.png";

		+INVENTORY.FANCYPICKUPSOUND
		+INVENTORY.INVBAR
	}
	States
	{
	Spawn:
		MODK A 5 Bright;
		MODK A 30;
		Loop;
	Use:
		TNT1 A 0 Arg_A_UseModKit();
		Stop;
	}
	
	/*
	 * Mod Kit Interface of Awesome.
	 */
	action bool Arg_A_UseModKit()
	{
		// First, check if we already have all mods. If so, trade the kit for
		// a backpack and return.
		// [TODO] replace ArgModTotal with a player variable
		// [TODO] remove this shortcut entirely when Equipment arrives.
		if(self.CountInv("ArgModTotal") == 12) {
			A_GiveInventory("Backpack", 1);
			A_TakeInventory("ArgModKit", 1);
			A_PlaySound("items/modkit/pickup", CHAN_AUTO);
			return true; // remove the item.

		} else if (invoker.active) {
			// kit already active. Deactivate it.
			invoker.active = false;

		} else {
			// kit not yet active. Activate it.
			invoker.active = true;
		}
		return false; // do not remove if just activating/deactivating the kit.
	}
	
	/*
	 * SPOOOOOON!
	 */
	override void Tick()
	{
		Super.Tick();
		let player = ArgPlayer(self.owner);

		// allow players to dismiss the mod kit menu by pressing 'use'
		if(self.active && player && (player.GetPlayerInput(INPUT_BUTTONS) & BT_USE)) {
			self.active = false;
		}
	}
}
