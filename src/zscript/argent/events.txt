// --------------------------------------------------------------------------
//
// ARGENT: Event Handlers
//
// --------------------------------------------------------------------------

class ArgEventHandler : StaticEventHandler
{
	/*
	 * Do stuff when the world exists.
	 */
	override void WorldLoaded(WorldEvent e)
	{
		// initialize common vars so it's accessible in UI scope.
		ArgCommon._Init();
	}

	/*
	 * Do stuff for each player spawned in the map.
	 */
	override void PlayerEntered(PlayerEvent e)
	{
		let player = ArgPlayer(players[e.PlayerNumber].mo);
		if(player) {
			// always reset the player's speed back to normal on map start.
			// this way you don't get stuck 
			// [TODO] maybe make this a bit smarter and don't assume
			//        the player's speed is always 1.0, just in case another mod
			//        decides to change it or whatnot.
			player.speed = 1.0;

			// dismiss any active modkit menus
			let modkit = ArgModKit(player.FindInventory('ArgModKit'));
			if(modkit) {
				modkit.active = false;
			}
		}
	}

	/*
	 * These comments are pretty redundant, TBH.
	 */
	override void RenderOverlay(RenderEvent e)
	{
		// fancy crosshair augments. don't draw 'em if default crosshairs are forced.
		if(e.camera && e.camera.player && !crosshairforce) {
			let weap = ArgWeapon(e.camera.player.ReadyWeapon);
			if(weap) {
				weap.Arg_DrawCrosshairAugment(e.viewPos, e.viewAngle, e.viewPitch, e.viewRoll, e.fracTic);
			}
		}
	}
}
