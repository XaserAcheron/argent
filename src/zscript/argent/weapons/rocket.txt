// --------------------------------------------------------------------------
//
// ARGENT: Rocket Launcher
//
// --------------------------------------------------------------------------

class ArgRocketLauncher : ArgModWeapon replaces RocketLauncher
{
	const ROCKET_OVERLAY_LAYER = 2;

	override void BeginPlay()
	{
		Super.BeginPlay();

		mod[1].ammoUse = 1;

		mod[2].ammoUse = 3;
		mod[2].charge.init(0, 20);
	}
	
	Default
	{
		Weapon.SelectionOrder 7;
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 2;
		Weapon.AmmoType "ArgRocketAmmo";
		Inventory.Icon "graphics/hud/weapon-rocket.png";
		Inventory.PickupMessage "$ARG_GOT_ROCKET";
		Tag "$ARG_TAG_ROCKET";

		+WEAPON.NOAUTOFIRE
	}
	States
	{
	Spawn:
		RKLP A -1;
		Stop;
	Ready:
		RKLG A 0;
		Goto Super::Ready;
	Deselect:
		"####" A 0 {
			A_StopSound(CHAN_5);
			invoker.mod[2].charge.toMin();
			Arg_A_RocketLockClear();
		}
		Goto DeselectLoop;

	/* Unmodded + Primary */

	Mod0Select:
		RKLG A 0;
		Goto SelectLoop;
	Mod0Ready:
		RKLG A 0;
		Goto ReadyLoop;
	Mod0Fire:
		"####" B 1 Bright {
			Arg_A_GunFlash('Flash', "weapons/rocket/fire");
			if(invoker.modSelected == 1) {
				A_FireProjectile("ArgRocketDetonate", 0, 1, 4.0 + frandom(-0.50, 0.50), frandom(-0.50, 0.50));
			} else {
				A_FireProjectile("ArgRocket"        , 0, 1, 4.0 + frandom(-0.50, 0.50), frandom(-0.50, 0.50));
			}
			Arg_A_WeaponOffset(6.0, 3.0);
		}
		"####" C 1 Arg_A_WeaponOffset(16.0,  8.0);
		"####" D 1 Arg_A_WeaponOffset(20.0, 10.0);
		"####" E 1 Arg_A_WeaponOffset(22.0, 11.0);
		"####" F 1 Arg_A_WeaponOffset(23.0, 11.5);
		"####" G 1;
		"####" A 1;
		"####" A 1 Arg_A_WeaponOffset(22.0, 11.0 );
		"####" A 1 Arg_A_WeaponOffset(20.0, 10.0 );
		"####" H 1 Arg_A_WeaponOffset(17.0,  8.5 );
		"####" H 1 Arg_A_WeaponOffset(13.0,  6.5 );
		"####" I 1 Arg_A_WeaponOffset(10.0,  5.0 );
		"####" J 1 Arg_A_WeaponOffset( 7.0,  3.5 );
		"####" K 1 Arg_A_WeaponOffset( 5.0,  2.5 );
		"####" L 1 Arg_A_WeaponOffset( 3.0,  1.5 );
		"####" M 1 Arg_A_WeaponOffset( 2.0,  1.0 );
		"####" N 1 Arg_A_WeaponOffset( 1.0,  0.5 );
		"####" O 1 Arg_A_WeaponOffset( 0.0,  0.0 );
		"####" P 1;
		"####" Q 5 A_CheckReload;
		"####" Q 1 Arg_A_WeaponOffset(-0.25, 0.25);
		"####" R 1 Arg_A_WeaponOffset(-0.5,  0.5 );
		"####" S 1 Arg_A_WeaponOffset(-1.5,  1.5 );
		"####" T 1 Arg_A_WeaponOffset(-2.5,  2.5 );
		"####" U 1 Arg_A_WeaponOffset(-3.5,  3.5 );
		"####" V 4 Arg_A_WeaponOffset(-4.0,  4.0 );
		"####" W 1 Arg_A_WeaponOffset(-3.5,  3.5 );
		"####" X 1 Arg_A_WeaponOffset(-2.5,  2.5 );
		"####" Y 1 Arg_A_WeaponOffset(-1.5,  1.5 );
		"####" Z 1 Arg_A_WeaponOffset(-0.5,  0.5 );
		"####" [ 1 Arg_A_WeaponOffset( 0.0,  0.0 );
		"####" A 0 A_Refire();
		Goto Ready;
	Flash:
		TNT1 A 1 Bright A_Light(1);
		TNT1 A 2 Bright A_Light(2);
		TNT1 A 1 Bright A_Light(1);
		Goto LightDone;

	/* Mod 1: Remote Detonator */
	
	Mod1Select:
		RK1G A 0;
		Goto SelectLoop;
	Mod1Raise:
		RKLG A 0 {
			A_PlaySound("weapons/rocket/mod1swap", CHAN_AUTO);
			A_WeaponOffset(4.0, 32.0, WOF_KEEPY);
		}
		Goto ModSwapUp;
	Mod1Swap:
		RK1S S 1 Arg_A_WeaponOffset( 5.0, 2.0);
		RK1S R 1 Arg_A_WeaponOffset( 6.0, 5.0);
		RK1S Q 1 Arg_A_WeaponOffset( 8.0, 7.0);
		RK1S P 1 Arg_A_WeaponOffset(11.0, 9.0);
		RK1S P 1 Arg_A_WeaponOffset(13.0, 8.0);
		RK1S P 5 Arg_A_WeaponOffset(14.0, 7.0);
		RK1S O 1 Arg_A_WeaponOffset(13.0, 6.5);
		RK1S N 1 Arg_A_WeaponOffset(11.0, 5.5);
		RK1S M 1 Arg_A_WeaponOffset( 9.0, 4.5);
		RK1S L 1 Arg_A_WeaponOffset( 7.0, 3.5);
		RK1S J 1 Arg_A_WeaponOffset( 5.0, 2.5);
		RK1S H 1 Arg_A_WeaponOffset( 3.0, 1.5);
		RK1S F 1 Arg_A_WeaponOffset( 1.0, 0.5);
		RK1S E 4 Arg_A_WeaponOffset( 0.0, 0.0);
		RK1S D 1;
		RK1S C 1;
		RK1S B 1;
		RK1S A 1;
	Mod1Ready:
		RK1G A 0 A_Overlay(ROCKET_OVERLAY_LAYER, "Mod1Overlay");
		Goto ReadyLoop;
	Mod1Overlay:
		TNT1 A 1 Arg_A_SynthAltFire("Mod1Detonate");
		Loop;
	Mod1Detonate:
		TNT1 A 0 Arg_A_DetonateRockets();
	Mod1DetonateHold:
		TNT1 A 1;
		TNT1 A 0 Arg_A_SynthAltFire("Mod1DetonateHold");
		Goto Mod1Overlay;

	/* Mod 2: Lock-on Burst */
	
	Mod2Select:
		RK2G A 0;
		Goto SelectLoop;
	Mod2Raise:
		RKLG A 0 {
			A_Overlay(ROCKET_OVERLAY_LAYER);
			A_PlaySound("weapons/rocket/mod2swap", CHAN_AUTO);
			A_WeaponOffset(4.0, 32.0, WOF_KEEPY);
		}
		Goto ModSwapUp;
	Mod2Swap:
		RK2S N 1 Arg_A_WeaponOffset( 5.0, 2.0);
		RK2S M 1 Arg_A_WeaponOffset( 6.0, 5.0);
		RK2S L 1 Arg_A_WeaponOffset( 8.0, 7.0);
		RK2S K 1 Arg_A_WeaponOffset(11.0, 9.0);
		RK2S K 1 Arg_A_WeaponOffset(13.0, 8.0);
		RK2S K 5 Arg_A_WeaponOffset(14.0, 7.0);
		RK2S J 1 Arg_A_WeaponOffset(13.0, 6.5);
		RK2S I 1 Arg_A_WeaponOffset(11.0, 5.5);
		RK2S H 1 Arg_A_WeaponOffset( 9.0, 4.5);
		RK2S G 1 Arg_A_WeaponOffset( 7.0, 3.5);
		RK2S F 1 Arg_A_WeaponOffset( 5.0, 2.5);
		RK2S E 1 Arg_A_WeaponOffset( 3.0, 1.5);
		RK2S D 1 Arg_A_WeaponOffset( 1.0, 0.5);
		RK2S C 1 Arg_A_WeaponOffset( 0.0, 0.0);
		RK2S B 4 Arg_A_WeaponOffset( 0.0, 0.0);
		RK2S A 2;
	Mod2Ready:
		RK2G A 0;
		Goto ReadyLoop;
	Mod2Up:
		RK2G A 1 A_JumpIfTargetInLOS("Mod2LockStart", 0, JLOSF_COMBATANTONLY);
		RK2G A 0 A_Refire();
		Goto ReadyLoop;
	Mod2LockStart:
		RK2G A 0 {
			Arg_A_RocketLockOn();
			A_PlaySound("weapons/rocket/mod2up", CHAN_6);
			invoker.mod[2].charge.inc();
		}
	Mod2Hold:
		RK2G A 1 {
			// check to see if the player has any charge. If none, leap back
			// to Mod2Up as we haven't acquired a valid target yet.
			if(invoker.mod[2].charge.isMin()) {
				return ResolveState("Mod2Up");

			// got enough charge and the button's down? fire away!
			} else if (invoker.mod[2].charge.isMax() && Arg_SynthFire()) {
				return ResolveState("Mod2Fire");
			}
			invoker.mod[2].charge.inc();
			return ResolveState(null);
		}
		RK2G A 0 A_Refire();
	Mod2Down:
		RK2G A 0 {
			invoker.mod[2].charge.toMin();
			Arg_A_RocketLockClear();
			A_StopSound(CHAN_6);
		}
		Goto Ready;
	Mod2Fire:
		RK2G C 1 Bright {
			invoker.mod[2].charge.toMin();
			A_GunFlash();
			A_TakeInventory("ArgRocketAmmo", 1, TIF_NOTAKEINFINITE);
			A_FireProjectile("ArgRocketLockOn", 0, 0, 4.0 + frandom(-2.50, 0.50), frandom(-2.50, 0.50));
			A_PlaySound("weapons/rocket/fire", CHAN_5);
			A_AlertMonsters();
			Arg_A_WeaponOffset(6.0, 3.0);
			return ResolveState(null);
		}
		RK2G D 1 Arg_A_WeaponOffset(16.0,  8.0);
		RK2G E 1 Arg_A_WeaponOffset(22.0, 11.0);
		RK2G F 1 Arg_A_WeaponOffset(20.0, 10.0);
		RK2G D 1 Bright {
			A_GunFlash();
			A_TakeInventory("ArgRocketAmmo", 1, TIF_NOTAKEINFINITE);
			A_FireProjectile("ArgRocketLockOn", 0, 0, 4.0 + frandom(-2.50, 0.50), frandom(-2.50, 0.50));
			A_PlaySound("weapons/rocket/altfire", CHAN_6);
			A_AlertMonsters();
			Arg_A_WeaponOffset(12.0, 6.0);
		}
		RK2G E 1 Arg_A_WeaponOffset(20.0, 10.0);
		RK2G F 1 Arg_A_WeaponOffset(23.0, 11.5);
		RK2G G 1 Arg_A_WeaponOffset(22.0, 11.0);
		RK2G B 1 Bright {
			A_GunFlash();
			A_TakeInventory("ArgRocketAmmo", 1, TIF_NOTAKEINFINITE);
			A_FireProjectile("ArgRocketLockOn", 0, 0, 4.0 + frandom(-2.50, 0.50), frandom(-2.50, 0.50));
			A_PlaySound("weapons/rocket/fire", CHAN_5);
			A_AlertMonsters();
			Arg_A_WeaponOffset(14.0, 7.0);
		}
		RK2G C 1 Arg_A_WeaponOffset(16.0,  8.0);
		RK2G D 1 Arg_A_WeaponOffset(20.0, 10.0);
		RK2G E 1 Arg_A_WeaponOffset(22.0, 11.0);
		RK2G F 1 Arg_A_WeaponOffset(23.0, 11.5);
		RK2G G 1;
		RK2G A 1;
		RK2G A 1 Arg_A_WeaponOffset(22.0, 11.0 );
		RK2G A 1 Arg_A_WeaponOffset(20.0, 10.0 );
		RK2G A 1 Arg_A_WeaponOffset(17.0,  8.5 );
		RK2G A 1 Arg_A_WeaponOffset(13.0,  6.5 );
		RK2G A 1 Arg_A_WeaponOffset(10.0,  5.0 );
		RK2G H 1 Arg_A_WeaponOffset( 7.0,  3.5 );
		RK2G H 1 Arg_A_WeaponOffset( 5.0,  2.5 );
		RK2G I 1 Arg_A_WeaponOffset( 3.0,  1.5 );
		RK2G I 1 Arg_A_WeaponOffset( 2.0,  1.0 );
		RK2G J 1{Arg_A_WeaponOffset( 1.0,  0.5 ); A_PlaySound("weapons/rocket/mod2load", CHAN_5); }
		RK2G J 1 Arg_A_WeaponOffset( 0.0,  0.0 );
		RK2G K 1 A_CheckReload();
		RK2G LMNOP 1;
		RK2G Q 1 Arg_A_WeaponOffset(-0.25, 0.25);
		RK2G R 1 Arg_A_WeaponOffset(-0.5,  0.5 );
		RK2G S 1 Arg_A_WeaponOffset(-1.5,  1.5 );
		RK2G T 1 Arg_A_WeaponOffset(-2.5,  2.5 );
		RK2G U 1 Arg_A_WeaponOffset(-3.5,  3.5 );
		RK2G V 1 Arg_A_WeaponOffset(-4.0,  4.0 );
		RK2G W 1 Arg_A_WeaponOffset(-3.5,  3.5 );
		RK2G X 1 Arg_A_WeaponOffset(-2.5,  2.5 );
		RK2G Y 1 Arg_A_WeaponOffset(-1.5,  1.5 );
		RK2G Z 1 Arg_A_WeaponOffset(-0.5,  0.5 );
		RK2G [ 1 Arg_A_WeaponOffset( 0.0,  0.0 );
		RK2G A 0 A_Refire();
		Goto Ready;
	}
	
	/*
	 * Remote-detonates all rockets. Aww yeah!
	 */
	action void Arg_A_DetonateRockets() {
		let player = ArgPlayer(self);
		if(player) {
			bool didDetonate = false;

			// pretty simple stuff. just loop through the player's rocket array
			// and detonate anything we see.
			for(int i = 0; i < player.numRockets; i++) {
				let rocket = ArgRocketDetonate(player.rockets[i]);
				if(rocket && !rocket.detonated) {
					didDetonate = true;
					rocket.SetStateLabel('Detonate');
				}
				player.rockets[i] = null;
			}
			player.numRockets = 0;

			A_PlaySound(didDetonate ? "weapons/rocket/mod1yep" : "weapons/rocket/mod1nope", CHAN_7);
		}
	}
	
	/*
	 * Set target lock.
	 */
	action void Arg_A_RocketLockOn() {
		let player = ArgPlayer(self);
		if(player) {
			// get the target currently under the player's crosshair (if any)
			// and store it in the player var so we can use it later.
			FTranslatedLineTarget t;
			AimLineAttack(self.angle, MISSILERANGE, t, 0.0, ALF_PORTALRESTRICT);
			if(t.linetarget) {
				player.lockTarget = t.linetarget;
			}
		}
	}
	
	/*
	 * Clear target lock.
	 */
	action void Arg_A_RocketLockClear() {
		let player = ArgPlayer(self);
		if(player) {
			player.lockTarget = null;
		}
	}
}

class ArgRocket : ArgRocketBase
{
	Default
	{
		Radius 9;
		Damage 30;
		Speed 30;
		DamageType "ArgPlayerRocket";
		Obituary "$ARG_OB_ROCKET";
	
		+EXTREMEDEATH
	}
}

class ArgRocketLockOn : ArgRocket
{
	/*
	 * On spawn, set the rocket's tracer to the player's lock-on target.
	 */
	override void PostBeginPlay() {
		let player = ArgPlayer(self.target);
		if(player && player.lockTarget) {
			self.tracer = player.lockTarget;
		}
	}

	Default
	{
		+SEEKERMISSILE
	}
	States
	{
	Spawn:
		MISL A 1 Bright A_JumpIfTargetInLOS("Seek", 0, JLOSF_PROJECTILE);
		Loop;
	Seek: 
		MISL A 1 Bright A_SeekerMissile(5, 10);
		Goto Spawn;
	}
}

class ArgRocketDetonate : ArgRocket
{
	bool detonated;
	
	/*
	 * On spawn, add the rocket to the player array so we can nuke it later.
	 *
	 * [XA] normally I'd like to do this when spawning the rocket, but
	 *      I'd have to replicate a bunch of A_FireProjectile to do so,
	 *      which I'll wait on for a bit.
	 */
	override void PostBeginPlay() {
		super.PostBeginPlay();

		// [XA] Presently, if for you have more than the max number of rockets
		//      out, the most recently fired one will not be detonate-able.
		//      This is kinda dumb, but this situation is hella rare so I'm
		//      taking the lazy-Xaser route. D:
		let player = ArgPlayer(self.target);
		if(player && player.numRockets < player.MAX_ROCKETS) {
			player.rockets[player.numRockets] = self;
			player.numRockets++;
		}
	}
	
	/*
	 * FastProjectile trail effect override: don't spawn a trail if we've gone boom.
	 */
	override void Effect() {
		if(!detonated) {
			super.Effect();
		}
	}

	States
	{
	Detonate:
		REXP A 4 Bright {
			detonated = true;
			A_Stop();
			A_SetScale(1.0);
			A_Explode(240, 192, 1, 0, 64);
			A_PlaySound("weapons/rocket/explode", CHAN_VOICE);
		}
		REXP C 3 Bright { A_SetScale(1.2, 1.0); A_SetTranslucent(0.7); }
		REXP D 2 Bright { A_SetScale(1.4, 1.0); A_SetTranslucent(0.6); }
		REXP E 2 Bright { A_SetScale(1.5, 1.0); A_SetTranslucent(0.5); }
		REXP F 2 Bright { A_SetScale(1.6, 1.0); A_SetTranslucent(0.4); }
		Stop;
	Death:
		REXP A 4 Bright {
			detonated = true;
			A_SetScale(1.0);
			A_Explode(200, 128, 1);
		}
		Goto Super::Boom;
	}
}
