// --------------------------------------------------------------------------
//
// ARGENT: Gauss Cannon
//
// --------------------------------------------------------------------------

CONST int GAUSS_MOD1_AMMOUSE = 40;
CONST int GAUSS_MOD2_AMMOUSE = 50;
CONST int GAUSS_MOD_CHARGE   = 30;

CONST INT GAUSS_CROSSHAIR_ANIM1 = 74;
CONST INT GAUSS_CROSSHAIR_ANIM2 = 73;
CONST INT GAUSS_CROSSHAIR_ANIM3 = 72;
CONST INT GAUSS_CROSSHAIR_ANIM4 = 71;
CONST INT GAUSS_CROSSHAIR_IDLE  = 70;

ACTOR ArgGaussCannonModSelected : ArgModToken {}
ACTOR ArgGaussCannonMod1Enabled : ArgToken    {}
ACTOR ArgGaussCannonMod2Enabled : ArgToken    {}
ACTOR ArgGaussCannonModCharge   : ArgToken    { Inventory.MaxAmount 71 } // 30 is the damage max, but this is higher for sound looping purposes
ACTOR ArgGaussCannonModCooldown : ArgToken    {}

ACTOR ArgGaussCannon : ArgModWeapon
{
	Weapon.UpSound "weapons/gauss/equip"
	Weapon.SelectionOrder 3
	Weapon.AmmoUse 20
	Weapon.AmmoGive 40
	Weapon.AmmoType "Cell"
	Inventory.Icon "graphics/hud/weapon-gauss.png"
	Inventory.PickupMessage "$ARG_GOT_GAUSS"
	Tag "$ARG_TAG_GAUSS"
	Obituary "$ARG_OB_GAUSS"
	Decal "Scorch" // need to put this on the weapon since it doesn't work on rail puffs; dammit Xaser D:
	
	States
	{
	Spawn:
		GAUS A -1
		Stop
	Ready:
		GAUG A 0
		Goto Super::Ready

	/* Unmodded + Primary */

	Mod0Select:
		GAUG A 0
		Goto SelectLoop
	Mod0Swap:
		GAUG A 0
		Goto ModSwapUp
	Mod0Ready:
		GAUG FFFFFFFFEEDDCCBBAABBCCDDEE 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Mod0Fire:
		"####" G 1 Bright {
			A_GunFlash;
			A_Recoil(7.5);
			A_RailAttack (10*random(1,8), 8, 0, "none", "none", RGF_SILENT | RGF_NOPIERCING, 4, "ArgGaussPaul", 0, 0, 0, 0, 16, 0, "none"         , 0);
			A_RailAttack (10*random(1,8), 8, 0, "none", "none", RGF_SILENT | RGF_NOPIERCING, 4, "ArgGaussPaul", 0, 0, 0, 0, 16, 0, "none"         , 0);
			A_RailAttack (10*random(1,8), 8, 0, "none", "none", RGF_SILENT | RGF_NOPIERCING, 4, "ArgGaussPaul", 0, 0, 0, 0, 16, 0, "none"         , 0);
			A_RailAttack (10*random(1,8), 8, 0, "none", "none", RGF_SILENT | RGF_NOPIERCING, 4, "ArgGaussPaul", 0, 0, 0, 0, 16, 0, "none"         , 0);
			A_RailAttack (10*random(1,8), 8, 0, "none", "none", RGF_SILENT | RGF_NOPIERCING, 4, "ArgGaussPaul", 0, 0, 0, 0, 16, 0, "none"         , 0);
			A_RailAttack (10*random(1,8), 8, 0, "none", "none", RGF_SILENT | RGF_NOPIERCING, 4, "ArgGaussPaul", 0, 0, 0, 0, 16, 0, "none"         , 0);
			A_RailAttack (10*random(1,8), 8, 0, "none", "none", RGF_SILENT | RGF_NOPIERCING, 4, "ArgGaussPaul", 0, 0, 0, 0, 16, 0, "none"         , 0);
			A_RailAttack (10*random(1,8), 8, 0, "none", "none", RGF_SILENT | RGF_NOPIERCING, 4, "ArgGaussPaul", 0, 0, 0, 0, 16, 0, "none"         , 0);
			A_RailAttack (10*random(1,8), 8, 0, "none", "none", RGF_SILENT | RGF_NOPIERCING, 4, "ArgGaussPaul", 0, 0, 0, 0, 16, 0, "none"         , 0);
			A_RailAttack (10*random(1,8), 8, 1, "none", "none", RGF_SILENT | RGF_NOPIERCING, 4, "ArgGaussPuff", 0, 0, 0, 0, 16, 0, "ArgGaussTrail", 0);
			A_AlertMonsters;
			A_PlaySound("weapons/gauss/fire", CHAN_5);
			A_WeaponOffset(0.0, 32.0);
		}
		"####" H 1 Bright A_WeaponOffset( 2.0, 34.0)
		"####" I 1 Bright A_WeaponOffset( 4.0, 38.0)
		"####" J 1 Bright A_WeaponOffset( 6.0, 42.0)
		"####" K 1 Bright A_WeaponOffset( 8.0, 44.5)
		"####" L 1 Bright A_WeaponOffset( 9.0, 46.5)
		"####" M 1        A_WeaponOffset( 9.5, 47.5)
		"####" M 1        A_WeaponOffset(10.0, 48.0)
		"####" N 1        A_WeaponOffset( 9.0, 47.0)
		"####" N 1        A_WeaponOffset( 8.0, 45.0)
		"####" O 1        A_WeaponOffset( 6.5, 42.0)
		"####" O 1        A_WeaponOffset( 5.0, 38.0)
		"####" P 1        A_WeaponOffset( 3.0, 35.0)
		"####" P 1        A_WeaponOffset( 2.0, 34.5)
		"####" Q 1        A_WeaponOffset( 1.0, 33.0)
		"####" Q 1        A_WeaponOffset( 0.5, 32.5)
		"####" R 1        A_WeaponOffset( 0.0, 32.0)
		"####" R 16 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		"####" ST 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		"####" A 17 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		"####" BCDE 2 A_WeaponReady(WRF_NOFIRE)
		"####" A 0 A_Refire
		Goto Ready
	Flash:
		TNT1 A 2 Bright A_Light1
		TNT1 A 2 Bright A_Light(3)
		TNT1 A 2 Bright A_Light2
		TNT1 A 2 Bright A_Light1
		Goto LightDone

	/* Mod 1: Precision Scope */
		
	Mod1Select:
		GA1G A 0
		Goto SelectLoop
	Mod1Raise:
		// Override ModSwapUp so we can start the hand animation early.
		GA1S A 1 A_WeaponOffset(0.0,116.0)
		GA1S A 1 A_WeaponOffset(0.0,104.0)
		GA1S A 1 A_WeaponOffset(0.0, 92.0)
		GA1S B 1 A_WeaponOffset(0.0, 80.0)
		GA1S C 1 A_WeaponOffset(0.0, 68.0)
		GA1S D 1 A_WeaponOffset(0.0, 56.0)
		GA1S E 1 A_WeaponOffset(0.0, 44.0)
	Mod1Swap:
		GA1S F 1 A_WeaponOffset( 0.0, 32.0)
		GA1S GHIJK 1
		GA1S L 3
		GA1S M 1
		GA1S N 1 A_PlaySound("weapons/gauss/mod1swap", CHAN_AUTO)
		GA1S O 1 A_WeaponOffset( 68 - 67,  87 - 87 + 32)
		GA1S P 1 A_WeaponOffset(-23 + 27,  97 - 96 + 32)
		GA1S P 1 A_WeaponOffset(-20 + 27, 100 - 96 + 32)
		GA1S P 1 A_WeaponOffset(-18 + 27, 102 - 96 + 32)
		GA1S P 1 A_WeaponOffset(-17 + 27, 103 - 96 + 32)
		GA1S P 2 A_WeaponOffset(-16 + 27, 104 - 96 + 32)
		GA1S P 1 A_WeaponOffset(-17 + 27, 103 - 96 + 32)
		GA1S P 1 A_WeaponOffset(-19 + 27, 101 - 96 + 32)
		GA1S P 1 A_WeaponOffset(-20 + 27, 100 - 96 + 32)
		GA1S Q 1 A_WeaponOffset( 80 - 75,  92 - 90 + 32)
		GA1S Q 1 A_WeaponOffset( 77 - 75,  91 - 90 + 32)
		GA1S R 1 A_WeaponOffset( 73 - 73,  96 - 96 + 32)
		GA1S S 1 A_WeaponOffset( 71 - 73,  96 - 96 + 32)
		GA1S T 1 A_WeaponOffset( 41 - 45,  97 - 96 + 32)
		GA1S U 1 A_WeaponOffset( 47 - 53,  99 - 96 + 32)
		GA1S V 1 A_WeaponOffset( 51 - 59, 102 - 96 + 32)
		GA1S W 1 A_WeaponOffset( 37 - 47, 105 - 96 + 32)
		GA1S X 1 A_WeaponOffset( 22 - 33, 108 - 96 + 32)
		GA1G A 1 A_WeaponOffset( 13 - 25, 110 - 96 + 32)
		GA1G A 1 A_WeaponOffset( 14 - 25, 108 - 96 + 32)
		GA1G A 1 A_WeaponOffset( 16 - 25, 105 - 96 + 32)
		GA1G A 1 A_WeaponOffset( 20 - 25, 102 - 96 + 32)
		GA1G A 1 A_WeaponOffset( 24 - 25,  97 - 96 + 32)
		GA1G A 1 A_WeaponOffset( 25 - 25,  96 - 96 + 32)
		GA1G BCDEFFFFFFFFFUUFFUUFFUUFFUU 1 A_WeaponReady(WRF_NOBOB|WRF_ALLOWRELOAD)
	Mod1Ready:
		GA1G FFFFFFFFEEDDCCBBAABBCCDDEE 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Mod1Up:
		GSCP A 1 {
			if(CountInv("Clip") < GAUSS_Mod1_AMMOUSE) { return state("Ready"); }
			A_PlaySound("weapons/gauss/mod1charge", CHAN_5);
			A_ZoomFactor(3.0);
			A_SetCrosshair(GAUSS_CROSSHAIR_ANIM1);
			A_GiveInventory("ArgGaussCannonModCharge", 3); // for the first few states....
			return state(0);
		}
		GSCP A 1 A_SetCrosshair(GAUSS_CROSSHAIR_ANIM2)
		GSCP A 1 A_SetCrosshair(GAUSS_CROSSHAIR_ANIM3)
		GSCP A 1 A_SetCrosshair(GAUSS_CROSSHAIR_ANIM4)
		GSCP A 1 A_SetCrosshair(GAUSS_CROSSHAIR_IDLE )
	Mod1Hold:
		GSCP A 1 {
			A_GiveInventory("ArgGaussCannonModCharge", 1);
			if(CountInv("ArgGaussCannonModCharge") == 70) {
				A_PlaySound("weapons/gauss/mod1hum", CHAN_5, 1.0, 1);
			}
			return A_JumpIf(CallACS("SynthFire"), "Mod1Fire");
		}
		GSCP A 0 A_Refire
	Mod1Down:
		GSCP A 1 {
			A_TakeInventory("ArgGaussCannonModCharge");
//			A_PlaySound("weapons/gauss/mod1down", CHAN_5);
			A_StopSound(CHAN_5);
			A_ZoomFactor(1.0);
		}
		GSCP A 1 A_SetCrosshair(GAUSS_CROSSHAIR_ANIM4)
		GSCP A 1 A_SetCrosshair(GAUSS_CROSSHAIR_ANIM3)
		GSCP A 1 A_SetCrosshair(GAUSS_CROSSHAIR_ANIM2)
		GSCP A 1 A_SetCrosshair(GAUSS_CROSSHAIR_ANIM1)
		GSCP A 0 A_SetCrosshair(0)
		Goto Ready
	Mod1Fire:
		GSCP A 1 {
			// check if you have enough ammo for a fully-charged shot; i may make
			// this more modular at some point so you can deliver a partial shot.
			if(CountInv("Cell") < GAUSS_MOD1_AMMOUSE) { return state("Mod1Down"); }
			
			// first, cap the charge amount at the actual max.
			if(CountInv("ArgGaussCannonModCharge") > GAUSS_MOD_CHARGE) {
				A_TakeInventory("ArgGaussCannonModCharge");
				A_GiveInventory("ArgGaussCannonModCharge", GAUSS_MOD_CHARGE);
			}
			
			// goddamn charge damage damn -- n rounds of 7d8; equals 210%
			// damage at full charge. Also take ammo -- kinda operates under the
			// assumption that the max is 40 cells and the charge is 30 iterations...
			A_TakeInventory("Cell", 10, TIF_NOTAKEINFINITE);
			while(CountInv("ArgGaussCannonModCharge") > 0) {
				// only spawn one trail, for sanity
				if(CountInv("ArgGaussCannonModCharge") == 1) {
					A_RailAttack (7*random(1,8), 0, 1, "none", "none", RGF_SILENT | RGF_NOPIERCING, 4, "ArgGaussPuff", 0, 0, 0, 0, 16, 0, "ArgGaussTrail", 0);
				} else {
					A_RailAttack (7*random(1,8), 0, 1, "none", "none", RGF_SILENT | RGF_NOPIERCING, 4, "ArgGaussPaul", 0, 0, 0, 0, 16, 0, "none"         , 0);
				}
				A_TakeInventory("ArgGaussCannonModCharge", 1);
				A_TakeInventory("Cell", 1, TIF_NOTAKEINFINITE);
			}
			
			// do the rest of the shooty stuff.
			A_PlaySound("weapons/gauss/mod1fire", CHAN_5);
			A_ZoomFactor(2.00);
			A_AlertMonsters;
			A_GunFlash;
			
			return state(0);
		}
		GSCP A 1 A_ZoomFactor(2.10)
		GSCP A 1 A_ZoomFactor(2.20)
		GSCP A 1 A_ZoomFactor(2.30)
		GSCP A 1 A_ZoomFactor(2.40)
		GSCP A 1 A_ZoomFactor(2.50)
		GSCP A 1 A_ZoomFactor(2.60)
		GSCP A 1 A_ZoomFactor(2.70)
		GSCP A 1 A_ZoomFactor(2.80)
		GSCP A 1 A_ZoomFactor(2.90)
		GSCP A 1 A_ZoomFactor(3.00)
		GSCP A 1 A_ClearRefire
		GSCP A 1 A_SetCrosshair(GAUSS_CROSSHAIR_ANIM4)
		GSCP A 1 A_SetCrosshair(GAUSS_CROSSHAIR_ANIM3)
		GSCP A 1 A_SetCrosshair(GAUSS_CROSSHAIR_ANIM2)
		GSCP A 1 A_SetCrosshair(GAUSS_CROSSHAIR_ANIM1)
		GSCP A 1 A_SetCrosshair(0)
		GA1G A 0 A_ZoomFactor(1.00)
		GA1G R 16 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		GA1G ST 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		GA1G A 17 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		GA1G BCDE 2 A_WeaponReady(WRF_NOFIRE)
		Goto Ready

	/* Mod 2: Siege Mode */

	}
}

ACTOR ArgGaussTrail : ArgTrail
{
	RenderStyle "Add"
	Alpha 1.0
	Scale 0.5
	States
	{
	Spawn:
		GAUT ABCD 2 Bright
		Stop
	}
}

ACTOR ArgGaussPuff : ArgPuff
{
	+ALWAYSPUFF
	+EXTREMEDEATH
	RenderStyle "Add"
	DamageType "ArgGaussCannon"
	Alpha 1.0
	Scale 0.75
	States
	{
	Spawn:
		GAUP A 2 Bright NoDelay A_PlaySound("weapons/gauss/impact")
		GAUP BCDEF 2 Bright
		Stop
	}
}

ACTOR ArgGaussPaul : ArgGaussPuff
{
	// it's Paul!
	States
	{
	Spawn:
		TNT1 A 0
		Stop
	}
}
