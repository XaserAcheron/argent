// --------------------------------------------------------------------------
//
// ARGENT: Blood FX
//
// --------------------------------------------------------------------------

ACTOR ArgBlood replaces Blood
{
    Mass   1
	Radius 1
	Height 1
	Alpha  1
	+LOWGRAVITY
	+NOBLOCKMAP
	+FLOORCLIP
	+NOTELEPORT
	+FORCEXYBILLBOARD
	States
	{
	Spawn:
		SPRY A 0
		SPRY A 0 A_PlaySound("effects/blood")
		SPRY AB 2
		SPRY CCC 0 A_SpawnItemEx("ArgBloodSpurt",0,0,0,0.2*random(-10,10),0.2*random(-10,10),0.3*random(5,15),0, SXF_TRANSFERTRANSLATION | SXF_CLIENTSIDE,0)
		SPRY CDEFG 2
		Stop
	}
}

ACTOR ArgBloodSpurt
{
	Health  4
	Radius  3
	Height  6
	Speed   0.1
	Gravity 0.5
	Scale   0.5
	Mass    0
	+doombounce
	+FLOORCLIP
	+DONTSPLASH
	+NOTELEPORT
	+NOBLOCKMAP 
	+THRUACTORS
	+FORCEXYBILLBOARD
	States
	{
	Spawn:
		BBLD ABCDABCDABCD 1 A_SpawnItemEx("ArgBloodTrail",0.05*random(-10,10),0.05*random(-10,10),0.05*random(-10,10),0,0,0,0, SXF_TRANSFERTRANSLATION | SXF_CLIENTSIDE,0)
		BLUD BBAA 1 A_SpawnItemEx("ArgBloodTrail",0.05*random(-10,10),0.05*random(-10,10),0.05*random(-10,10),0,0,0,0, SXF_TRANSFERTRANSLATION | SXF_CLIENTSIDE,0)
		Stop
	}
}

ACTOR ArgBloodTrail : ArgBloodSpurt
{
	Gravity 0.25
	States
	{
	Spawn:
		BLUD BBAA 2
		Stop
	}
}

ACTOR ArgBloodSplatter replaces BloodSplatter
{
    Mass   1
	Radius 0
	Height 0
	Alpha  1
	+NOGRAVITY
	+NOBLOCKMAP
	+FLOORCLIP
	+NOTELEPORT
	+THRUACTORS
	+FORCEXYBILLBOARD
	States
	{
	Spawn:
		SPRY A 0 
		SPRY A 2
		SPRY B 2 A_Stop
		SPRY CCCCCC 0 A_SpawnItemEx("ArgBloodTrail",0,0,0,0.2*random(-10,10),0.2*random(-10,10),0.3*random(5,15),0, SXF_TRANSFERTRANSLATION | SXF_CLIENTSIDE,0)
		SPRY CDEFG 2
		Stop
	}
}
