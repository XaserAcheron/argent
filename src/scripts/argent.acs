// --------------------------------------------------------------------------
//
// ARGENT: ACS Lib of Spectacular Scriptage
//
// Exceptionally-important note: This now requires GDCC to build.
// It's pretty much better than acc.exe in every way, so get it! :P
//
// [XA] As a note, this script got a lot hackier when the Mod Kit item was
//      introduced, but the plan is to refactor this all in zscript anyway,
//      so I figured I'd let a few shenanigans slide for now. Be warned. :P
//
// --------------------------------------------------------------------------

#library "argent"
#include "zcommon.acs"

#define DEF_STUN_SECONDS 5

/*
 * Init script. Gotta set those defaults to something sane.
 */ 
script "ArgentInit" enter {
	// Reset a couple of player properties.
	SetActorProperty(0, APROP_SPEED, 1.0);
	TakeInventory("ArgModKitToken", 1);
	
	// Give the player a BFG token on map start to signal zscript to
	// recalculate the BFG's ammotype. This will eventually be replaced
	// with a direct zscript function call once that's a thing.
	GiveInventory("ArgBFG9000AmmoToken", 1);
}

/*
 * Checks to see if the player has obtained all mods.
 */
function bool CheckModsFull (void) {
	// [XA] welcome to silly-assumption-ville. This ought to get
	//      better once ACS can call out to ZScript.
	return CheckInventory("ArgModTotal") == 12;
}

/*
 * Fanciful drawing functions for UseModKit below.
 */
#define MSG_TIME 0.05
#define MSG_FADE 0.20
function void DrawModPanel (int id, int x, str tag, str desc, str icon, str key, bool got) {

	// fixed-point conversion eeee
	x = (x * 1.0);
	
	// we sure do need some instructions
	int col = CR_UNTRANSLATED;
	int alp = 1.0;
	str sel;
	if(got) {
		sel = "This mod is already installed.";
		col = CR_BLACK;
		alp = 0.33;
	} else if (StrCmp(key, "") > 0) {
		sel = StrParam(s:"'", k:key, s:"' to install");
	}

	// draw the bocks
	SetHudSize(640, 480, 1);
	SetFont("graphics/hud/mod-base.png");
	HudMessage(s:"A" ; HUDMSG_FADEOUT, 1100 + id, CR_UNTRANSLATED, x +   0.1,   0.1, MSG_TIME, MSG_FADE);
	
	// draw uh eye-con
	SetFont(icon);
	HudMessage(s:"A" ; HUDMSG_FADEOUT | HUDMSG_ALPHA, 1000 + id, CR_UNTRANSLATED, x + 160.0, 180.0, MSG_TIME, MSG_FADE, alp);
	
	// draw some exceptionally text
	SetFont("ARGFONT");
	HudMessage(s:tag ; HUDMSG_FADEOUT, 1010 + id, col            , x + 160.0,  30.1, MSG_TIME, MSG_FADE);
	HudMessage(s:desc; HUDMSG_FADEOUT, 1020 + id, col            , x + 160.4, 320.1, MSG_TIME, MSG_FADE);
	HudMessage(s:sel ; HUDMSG_FADEOUT, 1030 + id, CR_RED         , x + 160.0, 400.1, MSG_TIME, MSG_FADE);
}
function void DrawModFooter (str text1, str text2, int time) {
	time = (time == 0) ? MSG_TIME : time; // TIME IS FLEETING. WHAT SAY YOU?

	// footer borks
	SetHudSize(640, 480, 1);
	SetFont("graphics/hud/mod-footer.png");
	HudMessage(s:"A"  ; HUDMSG_FADEOUT, 1042, CR_UNTRANSLATED, 320.0, 434.1, time, MSG_FADE);
	
	// australian rules footy text
	SetFont("ARGFONT");
	HudMessage(s:text1; HUDMSG_FADEOUT, 1040, CR_UNTRANSLATED, 320.0, 440.1, time, MSG_FADE);
	HudMessage(s:text2; HUDMSG_FADEOUT, 1041, CR_RED         , 320.0, 456.1, time, MSG_FADE);
}

/*
 * The Mod Kit! Shows a simple little menu thing that allows you to select
 * a mod to apply to your current gun. It's triggered when you use the mod
 * inventory item.
 */
script "UseModKit" (void) {
		
	// First, check if we already have all mods. If so, trade the kit for
	// a backpack and return.
	if (CheckModsFull()) {
		GiveInventory("Backpack", 1);
		TakeInventory("ArgModKit", 1);
		ActivatorSound("items/modkit/pickup", 255);
		DrawModFooter("All mods have already been purchased.", "Trading mod kit for a backpack full of ammo!", 4.0);
	
	// Next, check to see if we are already in the menu before proceeding. 
	// This way you can Use the kit item to enter/exit the menu.
	// Hooray parallelism!
	} else if(CheckInventory("ArgModKitToken") == 0) {
	
		// Give a token to indicate that the "menu" is up.
		GiveInventory("ArgModKitToken", 1);
		ActivatorSound("items/modkit/use", 255);
	
		// Loop for a bit.
		int input;
		do {
			input = GetPlayerInput(-1, INPUT_BUTTONS);

			// Snag the correct tags and icon names from LANGUAGE.
			// [XA] This nuttiness will go away once ZScript-conversion happens.
			//      Also why I'm not bothering to make this more efficient yet. :P
			str weapon     = GetWeapon();
			str weaponBase = StrMid(weapon, 3, 999);
			str jankCheck  = StrParam(s:"ARG_MOD_TAG_" , s:weaponBase, s:"1");
			str modTag1    = StrParam(l:jankCheck);
			str modTag2    = StrParam(l:StrParam(s:"ARG_MOD_TAG_" , s:weaponBase, s:"2"));
			str modDesc1   = StrParam(l:StrParam(s:"ARG_MOD_DESC_", s:weaponBase, s:"1"));
			str modDesc2   = StrParam(l:StrParam(s:"ARG_MOD_DESC_", s:weaponBase, s:"2"));
			str modIcon1   = StrParam(l:StrParam(s:"ARG_MOD_ICON_", s:weaponBase, s:"1"));
			str modIcon2   = StrParam(l:StrParam(s:"ARG_MOD_ICON_", s:weaponBase, s:"2"));
			str mod1Token  = "ArgMod1Enabled";
			str mod2Token  = "ArgMod2Enabled";
		
			// If we ain't got no mods, short-circuit a bit.
			// [XA] janky check against the language entry, heh
			if(StrCmp(modTag1, jankCheck) == 0) {
		
				// Draw a "can't mod this" panel.
				DrawModPanel(1, 160, "No Mods Available", "No mods available for this weapon.", "graphics/hud/mod-pic-nope.png", "", false);
			
			} else {
	
				// Draw mod panels. The Hudmessage bit is abstracted slightly, for sanity.
				DrawModPanel(1,   0, modTag1, modDesc1, modIcon1, "+attack"   , CheckInventory(mod1Token));
				DrawModPanel(2, 320, modTag2, modDesc2, modIcon2, "+altattack", CheckInventory(mod2Token));
			}
		
			// Draw footer with instructions n' such.
			DrawModFooter("Switch weapons to browse mods.", StrParam(s:"Press '", k:"+use", s:"' to exit."), 0);
		
			// delays are important
			Delay(1);
		
			// Break if the player presses 'use' or the token item is removed.
		} while (!(input & BT_USE) && CheckInventory("ArgModKitToken") > 0);
	}
	
	// Take away the token in case the player aborted above.
	ActivatorSound("items/modkit/dismiss", 255);
	TakeInventory("ArgModKitToken", 1);
}

/*
 * Plazma Stunz0rs
 */
script "NeuralStunner" (void) {
	// Get stun duration (in seconds) from the Stamina property.
	// If not set on this monster, use the default value.
	int stunSeconds = GetActorProperty(0, APROP_STAMINA);
	if( stunSeconds == 0 ) {
		stunSeconds = DEF_STUN_SECONDS;
	}
	int duration = stunSeconds * 35;
	int x, y, z, r;
	
	// loop for the duration and repeatedly set the pain state.
	// A custom "Pain.Stun" state is used; if this isn't defined,
	// the it falls back to the regular ol' Pain state.
	for(int i = 0; i < duration; i++) {

		// Don't SetActorState if the monster's dead,
		// or else Bad Things(tm) will happen.
		if(GetActorProperty(0, APROP_HEALTH) >= 1) {
			SetActorState(0, "Pain.Stun");
		} else {
			break;
		}
		
		// stunny zap effects, once per second.
		if(i % 35 == 0) {
			x = GetActorX(0);
			y = GetActorY(0);
			z = GetActorZ(0) + (GetActorProperty(0, APROP_HEIGHT) / 2);
			r = GetActorProperty(0, APROP_RADIUS);
			
			// spawn a different size actor depending on its radius.
			if(r <= 28.0) {
				Spawn("ArgStunEffect", x, y, z);
			} else if (r <= 56.0) {
				Spawn("ArgStunEffectLarge", x, y, z);
			} else {
				Spawn("ArgStunEffectHuge", x, y, z);
			}
		}
		
		Delay(1);
	}
}

/*
 * Silly Test Stuff
 */
script "FontTest" (void) {
	SetFont("ARGFONT");
	print(s:"The quick brown katefox jumps over the lazy xaserdog.\n");
}
